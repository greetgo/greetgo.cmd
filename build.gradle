def nowDate = new Date()
def nowFormattedDate = nowDate.format("yyyy-MM-dd/HH:mm:ss")
def vendorName = "greetgo!"
def ggRepo = System.getenv('GG_REPO')

buildscript {
  repositories {
    jcenter()
  }

  dependencies {
    classpath 'org.ajoberstar:grgit:1.1.0'
  }
}

allprojects {
  apply plugin: 'java'
  apply plugin: 'maven'
  apply plugin: 'idea'

  version = rootProject.file('version.txt').text.trim()

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  group = "kz.greetgo.cmd"

  ext {
    git = org.ajoberstar.grgit.Grgit.open(project.rootDir)
    revision = git.head().id
  }
}

subprojects {

  repositories {
    mavenLocal()
    if (ggRepo?.trim()) maven { url ggRepo.trim() }
    jcenter()
  }

  dependencies {
    ext.jacksonVersion = "2.7.3"
    ext.depinjectVersion = '2.0.0'
    ext.greetgoDbVersion = '2.0.0'
    ext.greetgoConfVersion = '0.1.5'
    ext.greetgoUtilVersion = "2.0.0"
    ext.jettyVersion = '9.4.0.v20161208'
    ext.guavaVersion = '23.0'

    testCompile 'org.testng:testng:6.5.1'
    compile 'org.easytesting:fest-assert-core:2.0M10'

  }

  jar {
    manifest {
      attributes(
        'Implementation-Title': project.name,
        'Implementation-Version': version,
        'Implementation-Vendor': vendorName,
        'Built-By': System.getProperty("user.name"),
        'Built-At': nowFormattedDate,
        'Specification-Version': revision,
      )
    }
  }

  sourceSets {
    main {
      java.srcDirs = ["src"]
      resources.srcDirs = ["src", "src_resources"]
    }
    test {
      java.srcDirs = ["test_src"]
      resources.srcDirs = ["test_src", "test_resources"]
    }
  }

  //noinspection GroovyMissingReturnStatement
  test { useTestNG() }
}
